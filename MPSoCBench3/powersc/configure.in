#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT(powersc, 0.9.1, klein@ic.unicamp.br)
AC_CONFIG_SRCDIR(autogen.sh)
AM_INIT_AUTOMAKE
AM_CONFIG_HEADER([config.h])

AC_LANG(C++)

AC_ARG_ENABLE(
         debug,
         [  --enable-debug          generate Makefile for debug mode],
         [CXXFLAGS="-ggdb3"],
         [CXXFLAGS="-O3"]
               )

AC_ARG_ENABLE(
         trace,
         [  --enable-trace          Turn on debug tracing (L1, L2, L3 or all)],
         [case "${enableval}"  in
            L1) trace="-DDEBUG_POWER_LEVEL1" ;;
            L2) trace="-DDEBUG_POWER_LEVEL2" ;;
            L3) trace="-DDEBUG_POWER_LEVEL3" ;;
            all) trace="-DDEBUG_POWER" ;;
            *)  AC_MSG_ERROR(bad value ${enable_val} for --enable-trace.  Run './configure --help' for details.) ;;
          esac],
         [trace="-DNO_DEBUG_POWER"]
               )
CXXFLAGS="$CXXFLAGS $trace"

AC_ARG_WITH(systemc,
   [  --with-systemc=DIR	  use SystemC Library from DIR],
   SCLIB_DIR="$withval",
   [AC_MSG_ERROR([You must specify the SystemC Library Directory. Run './configure --help' for details.])])

AC_ARG_WITH(examples,
   [  --with-examples         build the example designs],
   with_examples=true, with_examples=false)
AM_CONDITIONAL([WANT_EXAMPLES], [test x$with_examples = xtrue])

# set the SystemC headers directory
SCLIB_INCLUDES="-I$SCLIB_DIR/include"
SCLIB_SRC_INCLUDES="-I$SCLIB_DIR/src"
AC_SUBST(SCLIB_INCLUDES)
AC_SUBST(SCLIB_SRC_INCLUDES)

SCLIB_SRC_DIR="$SCLIB_DIR/src/sysc"
SC_DT_BIT_DIR="$SCLIB_SRC_DIR/datatypes/bit"
SC_COMM_DIR="$SCLIB_SRC_DIR/communication"
SC_KERNEL_DIR="$SCLIB_SRC_DIR/kernel"
AC_CHECK_FILES($SC_DT_BIT_DIR/sc_bit.cpp \
					$SC_DT_BIT_DIR/sc_logic.cpp \
					$SC_COMM_DIR/sc_clock.cpp \
					$SC_KERNEL_DIR/sc_wait.cpp \
					$SC_KERNEL_DIR/sc_join.cpp ,
					,
					AC_MSG_ERROR([SystemC source file not found!!!. See config.log for details.])
					)

AC_SUBST(SC_DT_BIT_DIR) 
AC_SUBST(SC_COMM_DIR)
AC_SUBST(SC_KERNEL_DIR)

# set the SystemC library directory
SCLIB_LIBDIR="$SCLIB_DIR/lib-linux"
LDFLAGS="-L$SCLIB_LIBDIR $LDFLAGS"

# Checks for libraries.
AC_CHECK_LIB([m], [pow])

# Checks for programs.
AC_PROG_CXX
#AC_PROG_CC

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

AC_CONFIG_FILES([Makefile src/Makefile examples/Makefile examples/led/Makefile examples/drink_machine/Makefile examples/count_zeros_seq/Makefile examples/dct/Makefile examples/dct-SMS/Makefile examples/Add32-gatelevel/Makefile examples/MulS16-rtl-macromodel/Makefile])
AC_PROG_RANLIB
AC_OUTPUT
